apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: "$rootDir/scripts/configs.gradle"

android {
    compileSdkVersion                   configs.compile_sdk
    defaultConfig {
        applicationId                   configs.application_id
        minSdkVersion                   configs.min_sdk
        targetSdkVersion                configs.target_sdk
        versionCode                     configs.version_code
        versionName                     configs.version_name
        testInstrumentationRunner       configs.instrument_runner

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
                arguments = [
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }

    /**
     * Currently we disable signing release config
     * because we don't have an existing release key file.
     * we will enable this function when we want to build
     * a release apk.
    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("secret/keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    } */

    buildTypes {
        release {
            minifyEnabled configs.minify_enabled
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            /**
             * Disable signing config for release apk.
             * we will enable this property when we want
             * to build with keystore file.*/
             // signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
        pro {
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                if (variant.productFlavors[0].name == "dev") {
                    def versionCode = defaultConfig.versionCode
                    def versionName = defaultConfig.versionName
                    def newName = "${configs.appName}_dev_${versionName}_${versionCode}.apk"
                    outputFileName = newName
                }
                if (variant.productFlavors[0].name == "pro") {
                    def versionCode = defaultConfig.versionCode
                    def versionName = defaultConfig.versionName
                    def newName = "${configs.appName}_pro_${versionName}_${versionCode}.apk"
                    outputFileName = newName
                }
            }
        }
    }
}

dependencies {
    //implementation ('com.sophoun.project.core:core-utils:0.2.6') { transitive = false }

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Test
    implementation "androidx.test:core:1.2.0"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation "junit:junit:4.12"
    // Design
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "com.google.android.material:material:1.1.0"
    implementation "com.github.bumptech.glide:glide:4.10.0"
    kapt "com.github.bumptech.glide:compiler:4.10.0"
    // Common
    implementation "org.jetbrains.anko:anko-commons:0.10.8"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.6.2"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "com.squareup.retrofit2:converter-gson:2.5.0"
    // Dagger
    def dagger_version = "2.23.2"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    // JetPack
    implementation "androidx.core:core-ktx:1.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.2.0"
    implementation "androidx.fragment:fragment-ktx:1.2.2"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    def room_version = "2.2.3"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // Coroutine
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"
    // others
    implementation "com.airbnb.android:lottie:3.0.7"
    // Test
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
    // Biometric
    implementation "androidx.biometric:biometric:1.0.1"
}